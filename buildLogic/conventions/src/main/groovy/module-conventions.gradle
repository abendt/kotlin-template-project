plugins {
    id 'kotlin-conventions'

    id("com.adarshr.test-logger")

    id 'jvm-test-suite'
}

dependencies {
    implementation(libs.kotlinLogging)

    testImplementation(platform(libs.junit.bom))
    testImplementation(libs.bundles.kotest)
    testImplementation(libs.mockk)

    runtimeOnly(libs.bundles.logback)
}

configurations {
    all {
        exclude module: "log4j"
        exclude module: "mockito-core"
        exclude module: "junit"
        exclude module: "commons-logging"
    }
}

def isIdea = providers.systemProperty("idea.version")
testlogger {
    // idea can't handle ANSI output
    setTheme(isIdea.isPresent() ? "plain" : "mocha")
    showFullStackTraces = false

    showStandardStreams = true
    showPassedStandardStreams = false
    showSkippedStandardStreams = false
    showFailedStandardStreams = true

    showPassed = true
    showSkipped = false
    showFailed = true
}

testing {
    suites {
        test {
            useJUnitJupiter("5.9.1")
        }
    }
}
